plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
    id "io.gitlab.arturbosch.detekt" version "1.0.0-RC14"
    id 'maven-publish'
}

group 'me.rotol.pupil'
version '0.1-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/zeromq/maven" }
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.pinterest:ktlint:0.33.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation 'org.zeromq:jeromq:0.5.1'
    implementation 'org.zeromq:czmq-jni:4.2.1'
    implementation 'org.zeromq:zyre-jni:2.0.1'

    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.8.0-beta4'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.8.0-beta4'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'timesync'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}

detekt {
    toolVersion = "1.0.0-RC14"
    input = files("src/main/kotlin")
    filters = ".*/resources/.*,.*/build/.*"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
